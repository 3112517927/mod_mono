AC_INIT(src/mod_mono.c)
AM_INIT_AUTOMAKE(mod_mono, 0.6.99)
AM_CONFIG_HEADER(include/mod_mono_config.h:config.in)


AC_PROG_CC
AM_PROG_LIBTOOL

AC_MSG_CHECKING([if building mod_mono_old is requested])
AC_ARG_ENABLE(old-module,
	[  --enable-old-module     enables compilation of the mod_mono_old module],
	enable_old_module=$enableval, enable_old_module=no)
AC_MSG_RESULT($enable_old_module)

# check for --with-apxs
AC_MSG_CHECKING(for --with-apxs)
AC_ARG_WITH(apxs, [  --with-apxs=PATH        Path to apxs],
[
  if test -x "$withval"
  then
    AC_MSG_RESULT([$withval executable, good])
    APXS=$withval
  else
    echo
    AC_MSG_ERROR([$withval not found or not executable])
  fi
],
AC_MSG_RESULT(no))

# check for --with-apr-config
# apr-config - at least in debian apr include directory is different
APR_INCLUDES=""
AC_MSG_CHECKING(for --with-apr-config)
AC_ARG_WITH(apr-config,
[  --with-apr-config=PATH  Path to apr-config (apache 2.0). You may use
                          this option when apr-config is not in the same
			  directory as apxs and the output of
			  'apr-config --includes' is different from
			  'apxs -q INCLUDEDIR'.
],
[
  if test -x "$withval"
  then
    AC_MSG_RESULT([$withval executable, good])
    APRCONFIG=$withval
  else
    echo
    AC_MSG_ERROR([$withval not found or not executable])
  fi
],
AC_MSG_RESULT(not specified))

# if no apxs found yet, check /usr/local/apache/sbin
# since it's the default Apache location
if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/local/apache/sbin)
  if test -x /usr/local/apache/sbin/apxs; then
    APXS=/usr/local/apache/sbin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
else
  if test -z "$APRCONFIG" ; then
    APRCONFIG=`dirname ${APXS}`/apr-config
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/local/apache/sbin)
  if test -x /usr/local/apache/sbin/apr-config; then
    APRCONFIG=/usr/local/apache/sbin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/local/apache2/bin)
  if test -x /usr/local/apache2/bin/apxs; then
    APXS=/usr/local/apache2/bin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/local/apache2/bin)
  if test -x /usr/local/apache2/bin/apr-config; then
    APRCONFIG=/usr/local/apache2/bin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/sbin)
  if test -x /usr/sbin/apxs; then
    APXS=/usr/sbin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/sbin)
  if test -x /usr/sbin/apr-config; then
    APRCONFIG=/usr/sbin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

# last resort
if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in your PATH)
  AC_PATH_PROG(APXS, apxs)
  if test -n "$APXS"; then
    AC_MSG_RESULT([found $APXS, we'll use this. Use --with-apxs to specify another.])
  fi
fi

if test -z "$APXS"; then
  AC_MSG_ERROR([**** apxs was not found, DSO compilation will not be available.])
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in your PATH)
  AC_PATH_PROG(APRCONFIG, apr-config)
  if test -n "$APXS"; then
    AC_MSG_RESULT([found $APRCONFIG, we'll use this. Use --with-apr-config to specify another.])
  fi
fi

# determine LIBEXEC
AC_MSG_CHECKING(for Apache libexec directory)
LIBEXECDIR=`${APXS} -q LIBEXECDIR`
AC_MSG_RESULT($LIBEXECDIR)

# determine INCLUDES

AC_MSG_CHECKING([for Apache include directory])
AP_INCLUDES="-I`${APXS} -q INCLUDEDIR`"
AC_MSG_RESULT($AP_INCLUDES)
CFLAGS="$CFLAGS $AP_INCLUDES"

AC_MSG_CHECKING([Apache version])
AC_TRY_COMPILE([#include <mpm.h> #include <apr.h>], [
	apr_byte_t b;
], [
	APACHE_VER=2.0
], [
	APACHE_VER=1.3
])

# Try apr-config
if test "$APACHE_VER" = "1.3" -a -x "$APRCONFIG"; then
	CFLAGS="$CFLAGS `$APRCONFIG --includes`"
	AC_TRY_COMPILE([#include <mpm.h> #include <apr.h>], [
		apr_byte_t b;
	], [
		APACHE_VER=2.0
	], [
		APACHE_VER=1.3
	])
fi

if test "$APACHE_VER" = "1.3"; then
	CFLAGS="$CFLAGS -DAPACHE13 -DEAPI"
fi

AC_MSG_RESULT($APACHE_VER)


AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
        AC_MSG_ERROR([You need to install pkg-config])
fi

PKG_PATH=
AC_ARG_WITH(crosspkgdir, [  --with-crosspkgdir=/path/to/pkg-config/dir],
        if test x$with_crosspkgdir = "x"; then
                if test -s $PKG_CONFIG_PATH; then
                        PKG_PATH=$PKG_CONFIG_PATH
                fi
        else
                PKG_PATH=$with_crosspkgdir
                PKG_CONFIG_PATH=$PKG_PATH
                export PKG_CONFIG_PATH
        fi
)

dnl Check for mono.pc if building mod_mono_old is requested
MONO_CFLAGS=""
MONO_LIBS=""
if test "yes" = "$enable_old_module" ; then
	MONO_REQUIRED_VERSION=0.28.99
	PKG_CHECK_MODULES(MONO,
			  mono >= $MONO_REQUIRED_VERSION,
			  enable_old_module="yes",
			  enable_old_module="no")

	if test "$enable_old_module" = "no" ; then
		AC_MSG_ERROR([Building old module requested but no mono.pc found. $MONO_PKG_ERRORS])
	fi
fi
AM_CONDITIONAL(BUILD_OLD_MODULE, test "yes" = "$enable_old_module")

AC_SUBST(LIBEXECDIR)
AC_SUBST(AP_INCLUDES)
AC_SUBST(CFLAGS)
AC_SUBST(MONO_CFLAGS)
AC_SUBST(MONO_LIBS)

AC_OUTPUT(Makefile src/Makefile)

echo "---"
echo "Configuration summary for mod_mono"
echo ""
echo "   * Installation prefix = $prefix"
echo "   * Apache version = $APACHE_VER"
echo "   * Apache modules directory = $LIBEXECDIR"
test -x "$APXS" && \
echo "   * apxs = $APXS"
test -x "$APRCONFIG" && \
echo "   * apr-config = $APRCONFIG"
echo "   * Build old module: $enable_old_module"
echo ""
echo "---"

