AC_INIT(src/mod_mono.c)
AM_INIT_AUTOMAKE(mod_mono, 1.1.9.1)
AM_CONFIG_HEADER(include/mod_mono_config.h:config.in)

AC_PROG_CC
AM_PROG_LIBTOOL

AC_C_BIGENDIAN
if test "x$ac_cv_c_bigendian" = "xyes" ; then
	AC_DEFINE([MODMONO_BIGENDIAN],,[Compiling on a big-endian machine.])
fi
	

# Checks for header files.
AC_HEADER_SYS_WAIT
AC_CHECK_HEADERS([netdb.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

# Checks for library functions.
AC_FUNC_FORK
AC_HEADER_STDC
AC_FUNC_SELECT_ARGTYPES
AC_CHECK_FUNCS([memset mkdir putenv setenv setrlimit select strcasecmp strerror strrchr dup2])

#
# --enable-debug
#
AC_MSG_CHECKING(if debug mode is requested)
AC_ARG_ENABLE(debug,
	[ ---enable-debug           build mod_mono in debug mode],
	enable_debug=$enableval, enable_debug=no)
AC_MSG_RESULT($enable_debug)
if test "x$enable_debug" = "xyes" ; then
	CFLAGS="$CFLAGS -DDEBUG"
fi


dnl AC_MSG_CHECKING([if building mod_mono_old is requested])
dnl AC_ARG_ENABLE(old-module,
dnl 	[  --enable-old-module     enables compilation of the mod_mono_old module],
dnl 	enable_old_module=$enableval, enable_old_module=no)
dnl AC_MSG_RESULT($enable_old_module)

# check for --with-apxs
AC_MSG_CHECKING(for --with-apxs)
AC_ARG_WITH(apxs, [  --with-apxs=PATH        Path to apxs],
[
  if test -x "$withval"
  then
    AC_MSG_RESULT([$withval executable, good])
    APXS=$withval
  else
    echo
    AC_MSG_ERROR([$withval not found or not executable])
  fi
],
AC_MSG_RESULT(no))

# check for --with-apr-config
# apr-config - at least in debian apr include directory is different
APR_INCLUDES=""
AC_MSG_CHECKING(for --with-apr-config)
AC_ARG_WITH(apr-config,
[  --with-apr-config=PATH  Path to apr-config (apache 2.0). You may use
                          this option when apr-config is not in the same
			  directory as apxs and the output of
			  'apr-config --includes' is different from
			  'apxs -q INCLUDEDIR'.
],
[
  if test -x "$withval"
  then
    AC_MSG_RESULT([$withval executable, good])
    APRCONFIG=$withval
  else
    if test "xno" = "x$withval" ; then
    	AC_MSG_RESULT([no])
    else
        AC_MSG_ERROR([$withval not found or not executable])
    fi
  fi
],
AC_MSG_RESULT(not specified))

# if no apxs found yet, check /usr/local/apache/sbin
# since it's the default Apache location
if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs2 in /usr/local/apache/sbin)
  if test -x /usr/local/apache/sbin/apxs2; then
    APXS=/usr/local/apache/sbin/apxs2
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/local/apache/sbin)
  if test -x /usr/local/apache/sbin/apxs; then
    APXS=/usr/local/apache/sbin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
else
  if test -z "$APRCONFIG" ; then
    APRCONFIG=`dirname ${APXS}`/apr-config
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/local/apache/sbin)
  if test -x /usr/local/apache/sbin/apr-config; then
    APRCONFIG=/usr/local/apache/sbin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])    
  else
    AC_MSG_RESULT(no)
    APRCONFIG=""
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs2 in /usr/local/apache2/bin)
  if test -x /usr/local/apache/bin/apxs2; then
    APXS=/usr/local/apache/bin/apxs2
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/local/apache2/bin)
  if test -x /usr/local/apache2/bin/apxs; then
    APXS=/usr/local/apache2/bin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/local/apache2/bin)
  if test -x /usr/local/apache2/bin/apr-config; then
    APRCONFIG=/usr/local/apache2/bin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])
  else
    AC_MSG_RESULT(no)
    APRCONFIG=""
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs2 in /usr/sbin)
  if test -x /usr/sbin/apxs2; then
    APXS=/usr/sbin/apxs2
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])    
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APXS"; then
  AC_MSG_CHECKING(for apxs in /usr/sbin)
  if test -x /usr/sbin/apxs; then
    APXS=/usr/sbin/apxs
    AC_MSG_RESULT([found, we'll use this. Use --with-apxs to specify another.])
  else
    AC_MSG_RESULT(no)
  fi
fi

if test -z "$APRCONFIG"; then
  AC_MSG_CHECKING(for apr-config in /usr/sbin)
  if test -x /usr/sbin/apr-config; then
    APRCONFIG=/usr/sbin/apr-config
    AC_MSG_RESULT([found, we'll use this. Use --with-apr-config to specify another.])
  else
    AC_MSG_RESULT(no)
    APRCONFIG=""
  fi
fi

# last resort
if test -z "$APXS"; then
  AC_PATH_PROG(APXS, apxs2)
fi

if test -z "$APXS"; then
  AC_PATH_PROG(APXS, apxs)
fi

if test -z "$APXS"; then
  AC_MSG_ERROR([**** apxs was not found, DSO compilation will not be available.])
fi

if test -z "$APRCONFIG"; then
  AC_PATH_PROG(APRCONFIG, apr-config)
fi

# determine LIBEXEC
AC_MSG_CHECKING(for Apache libexec directory)
APXS_LIBEXECDIR=`${APXS} -q LIBEXECDIR`
AC_MSG_RESULT($APXS_LIBEXECDIR)

AC_MSG_CHECKING(for Apache sysconf directory)
APXS_SYSCONFDIR=`${APXS} -q SYSCONFDIR`
AC_MSG_RESULT($APXS_SYSCONFDIR)
# determine INCLUDES

AC_MSG_CHECKING([for Apache include directory])
AP_INCLUDES="-I`${APXS} -q INCLUDEDIR`"
AC_MSG_RESULT($AP_INCLUDES)
CFLAGS="$CFLAGS $AP_INCLUDES"

AC_MSG_CHECKING([Apache version])
AC_TRY_COMPILE([
	#include <httpd.h>
], [
	char *version = AP_SERVER_BASEREVISION;
], [
	APACHE_VER=2.0
], [
	APACHE_VER=1.3
])

# ensure we have apache 1.3
if test "$APACHE_VER" = "1.3" ; then
	AC_TRY_COMPILE([
		#include <httpd.h>
	], [
		char *version = SERVER_BASEREVISION;
	], [
		APACHE_VER=1.3
	], [
		APACHE_VER=retry
	])
fi

# Try apr-config
if test "$APACHE_VER" = "retry" -a -x "$APRCONFIG"; then
	CFLAGS="$CFLAGS `$APRCONFIG --includes`"
	AC_TRY_COMPILE([
		#include <apr.h>
	], [
		apr_byte_t b;
	], [
		APACHE_VER=2.0
	], [
		APACHE_VER=1.3
	])
fi

if test "$APACHE_VER" = "retry"; then
	AC_MSG_ERROR([Confused on apache version. Please report this and send config.log])
fi

if test "$APACHE_VER" = "1.3"; then
	CFLAGS="$CFLAGS -DAPACHE13 -DEAPI"
fi

AC_MSG_RESULT($APACHE_VER)


AC_PATH_PROG(PKG_CONFIG, pkg-config, no)
if test "x$PKG_CONFIG" = "xno"; then
        AC_MSG_ERROR([You need to install pkg-config])
fi

PKG_PATH=
AC_ARG_WITH(crosspkgdir, [  --with-crosspkgdir=/path/to/pkg-config/dir],
        if test x$with_crosspkgdir = "x"; then
                if test -s $PKG_CONFIG_PATH; then
                        PKG_PATH=$PKG_CONFIG_PATH
                fi
        else
                PKG_PATH=$with_crosspkgdir
                PKG_CONFIG_PATH=$PKG_PATH
                export PKG_CONFIG_PATH
        fi
)

AM_CONDITIONAL(APACHE2, test "$APACHE_VER" = "2.0")
if test "$APACHE_VER" = "2.0" ; then
dnl Sources are recompiled if we change the target version
	AC_DEFINE([APACHE2],,[Compiling for Apache 2.0])
fi

# check for --with-mono-default-config-dir
AC_MSG_CHECKING(for --with-mono-default-config-dir)
DFLT_MONO_CONFIG_DIR=`$APXS -q SYSCONFDIR`/mod-mono-applications
AC_ARG_WITH(mono-default-config-dir,
[  --with-mono-default-config-dir=PATH        Default value for MonoApplicationsConfigDir],
[
  if test -x "$withval"
  then
    AC_MSG_RESULT([$withval])
    DFLT_MONO_CONFIG_DIR="$withval"
  else
    AC_MSG_RESULT([$DFLT_MONO_CONFIG_DIR])
  fi
],
AC_MSG_RESULT($DFLT_MONO_CONFIG_DIR))

found_apr_socket_connect=no
AC_CHECK_LIB(apr-0, apr_socket_connect, found_apr_socket_connect=yes)
if test "x$found_apr_socket_connect" = "xyes" ; then
	AC_DEFINE([HAVE_APR_SOCKET_CONNECT],,[apr_socket_connect is present])
fi

MONO_PREFIX=`$PKG_CONFIG --silence-errors --variable=prefix mono`
if test x$MONO_PREFIX = "x" ; then
	MONO_PREFIX=$prefix
fi
AC_MSG_CHECKING([for mono installation prefix])
AC_ARG_WITH(mono-prefix,
[  --with-mono-prefix=PATH  The prefix where mono is installed. It's used
                            to set the default paths for mod-mono-server.exe
			    and mono when the modules is installed in a
			    different prefix. Configuration directives can
			    override this.
],
        if test ! x$with_mono_prefix = "x" ; then
		if test ! -d "$with_mono_prefix" ; then
			AC_MSG_ERROR([Directory '$with_mono_prefix' not found])
		fi
		MONO_PREFIX=$with_mono_prefix
        fi
)

if test "$MONO_PREFIX" = NONE ; then
	MONO_PREFIX=$ac_default_prefix
fi
AC_MSG_RESULT($MONO_PREFIX)
AC_DEFINE_UNQUOTED([MONO_PREFIX],"$MONO_PREFIX",[Mono installation prefix])

CPPFLAGS="$CPPFLAGS $CFLAGS `$APXS -q EXTRA_CPPFLAGS`"
CFLAGS="$CFLAGS `$APXS -q EXTRA_CFLAGS`"
AC_CHECK_HEADER(http_protocol.h, \
	AC_DEFINE([HAVE_HTTP_PROTOCOL_H],1,\
	[Define to 1 if you have the <http_protocol.h> header file.]),,\
	[#include <httpd.h>])


AC_SUBST(APXS_LIBEXECDIR)
AC_SUBST(APXS_SYSCONFDIR)
AC_SUBST(APXS)
AC_SUBST(AP_INCLUDES)
AC_SUBST(CFLAGS)
AC_SUBST(DFLT_MONO_CONFIG_DIR)

AC_OUTPUT(
Makefile
mod_mono.conf
src/Makefile
man/mod_mono.8
man/Makefile
)

echo "---"
echo "Configuration summary for mod_mono"
echo ""
echo "   * Installation prefix = $prefix"
echo "   * Apache version = $APACHE_VER"
echo "   * Apache modules directory = $APXS_LIBEXECDIR"
test -x "$APXS" && \
echo "   * apxs = $APXS"
test "$APACHE_VER" = "2.0" -a -x "$APRCONFIG" && \
echo "   * apr-config = $APRCONFIG"
echo "   * Verbose logging (debug) = $enable_debug"
echo "   * mono prefix = $MONO_PREFIX"
echo "   * Default MonoApplicationsConfigDir = $DFLT_MONO_CONFIG_DIR"
dnl echo "   * Build old module: $enable_old_module"
echo ""
echo "---"

